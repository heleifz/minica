{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 使用 Cython 加速卷积层计算\n",
    "\n",
    "ND convolution 在 numpy 的支持比较有限，具体来说，对于 valid，full，same 等模式的支持不够好\n",
    "对于 backprop 时需要的特殊的卷积的支持也没有，所以我打算用 cython 实现一个。\n",
    "\n",
    "Cython 与 numpy 通信的机制是 typed memoryview，memoryview 通过取地址可以转换成对应的指针\n",
    "获取指针后，就可以在 C-level 做一些事情：\n",
    "\n",
    "1. 使用 memcpy 等操作来进行内存操作\n",
    "2. 使用 scipy.linalg.cython_blas 来做矩阵运算（benchmark needed）\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%load_ext Cython\n",
    "import numpy as np\n",
    "import scipy.ndimage\n",
    "import scipy.signal"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "在实现通用的 ND conv 之前，先对二维图像进行处理。"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%%cython\n",
    "\n",
    "# 首先对图像实现 im2col 的算法\n",
    "cimport cython\n",
    "# 利用 scipy 自带的 blas 做矩阵运算\n",
    "cimport scipy.linalg.cython_blas as blas\n",
    "cimport scipy.linalg.cython_lapack as lapack\n",
    "from cython cimport view\n",
    "\n",
    "# 支持 3d array 的 im2col\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cdef int im2col_core(double [:,:,:] src, double [:,:] dst,\n",
    "                     int kernel_h, int kernel_w) nogil:\n",
    "    cdef:\n",
    "        double *src_ptr = &src[0][0][0]\n",
    "        double *dst_ptr = &dst[0][0]\n",
    "\n",
    "        int c = src.shape[0]\n",
    "        int h = src.shape[1]\n",
    "        int w = src.shape[2]\n",
    "\n",
    "        int result_h = h - kernel_h + 1\n",
    "        int result_w = w - kernel_w + 1\n",
    "\n",
    "        int result_total = result_h * result_w\n",
    "        int filter_channel_total = kernel_h * kernel_w\n",
    "\n",
    "    if dst.shape[0] != filter_channel_total * c or \\\n",
    "       dst.shape[1] != result_total:\n",
    "        # dst array has different shape\n",
    "        return -1\n",
    "\n",
    "    # copy row by row\n",
    "    cdef:\n",
    "        int i, j, ch, n, r_index, img_offset\n",
    "        double *current_row\n",
    "        double *src_submatrix = src_ptr\n",
    "    for ch in range(c):\n",
    "        for i in range(kernel_h):\n",
    "            for j in range(kernel_w):\n",
    "                # 确定要拷贝的 image range\n",
    "                # 确定目标的行\n",
    "                r_index = ch * filter_channel_total + i * kernel_w + j\n",
    "                current_row = dst_ptr + r_index * result_total\n",
    "                src_submatrix = src_ptr + ch * w * h + i * w + j\n",
    "                # 采用 dlacpy 稍微加速拷贝 (~10%)\n",
    "                lapack.dlacpy('A', &result_w, &result_h,\n",
    "                              src_submatrix, &h, current_row, &result_h)\n",
    "    return 0\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cpdef int im2col_n_to_one_core(double [:,:,:] src, double [:,:] dst,\n",
    "                     int kernel_h, int kernel_w) nogil:\n",
    "    cdef:\n",
    "        double *src_ptr = &src[0][0][0]\n",
    "        double *dst_ptr = &dst[0][0]\n",
    "\n",
    "        int c = src.shape[0]\n",
    "        int h = src.shape[1]\n",
    "        int w = src.shape[2]\n",
    "\n",
    "        int result_h = h - kernel_h + 1\n",
    "        int result_w = w - kernel_w + 1\n",
    "\n",
    "        int result_total = result_h * result_w\n",
    "        int filter_channel_total = kernel_h * kernel_w\n",
    "\n",
    "    if dst.shape[0] != filter_channel_total or \\\n",
    "       dst.shape[1] != result_total * c:\n",
    "        # dst array has different shape\n",
    "        return -1\n",
    "\n",
    "    # copy row by row\n",
    "    cdef:\n",
    "        int i, j, ch, n, r_index, img_offset\n",
    "        double *current_row\n",
    "        double *src_submatrix = src_ptr\n",
    "        \n",
    "    for i in range(kernel_h):\n",
    "        for j in range(kernel_w):\n",
    "            # 确定要拷贝的 image range\n",
    "            # 确定目标的行\n",
    "            r_index = i * kernel_w + j\n",
    "            current_row = dst_ptr + r_index * result_total * c\n",
    "            src_submatrix = src_ptr + i * w + j\n",
    "            for ch in range(c):\n",
    "                # 采用 dlacpy 稍微加速拷贝 (~10%)\n",
    "                lapack.dlacpy('A', &result_w, &result_h,\n",
    "                              src_submatrix, &h, current_row, &result_h)\n",
    "                src_submatrix += w * h\n",
    "                current_row += result_w * result_h\n",
    "    return 0\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cpdef int conv_batch(double [:,:,:,:] src, double[:,:,:,:] dst,\n",
    "                     double [:,:,:,:] kernel, double [:,:] buf) nogil:\n",
    "\n",
    "    cdef int i\n",
    "    cdef int num = src.shape[0]\n",
    "    cdef int kn = kernel.shape[0]\n",
    "    cdef int kh = kernel.shape[2]\n",
    "    cdef int kw = kernel.shape[3]\n",
    "    cdef int dst_single_size = dst.shape[1] * dst.shape[2] * dst.shape[3]\n",
    "\n",
    "    cdef double *kernel_ptr = &kernel[0][0][0][0]\n",
    "    cdef double *dst_ptr = &dst[0][0][0][0]\n",
    "    cdef int h_buf, w_buf, h_vec, w_vec\n",
    "    # fortran 时顺序是反的\n",
    "    h_buf = buf.shape[1]\n",
    "    w_buf = buf.shape[0]\n",
    "    h_vec = w_buf\n",
    "    w_vec = kn\n",
    "    # 设置 stride\n",
    "    cdef int ld_buf = h_buf\n",
    "    cdef int ld_vec = h_vec\n",
    "    cdef int ld_out = buf.shape[1]\n",
    "    cdef double alpha = 1.0, beta = 0.0\n",
    "\n",
    "    # 每个图片做一次乘法(其实一个乘法也行，但是内存膨胀比较厉害)\n",
    "    for i in range(num):\n",
    "        if im2col_core(src[i], buf, kh, kw) != 0:\n",
    "            return -1\n",
    "        # 调用 blas 乘法 routine\n",
    "        blas.dgemm(\"N\", \"N\", &h_buf, &w_vec, &w_buf, &alpha,\n",
    "                   &buf[0][0], &ld_buf, kernel_ptr, &ld_vec,\n",
    "                   &beta, dst_ptr, &ld_out)\n",
    "        dst_ptr += dst_single_size\n",
    "    return 0\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cdef void copy_and_pad(double [:,:,:] src, double [:,:,:] dst, int pad_h, int pad_w) nogil:\n",
    "\n",
    "    cdef:\n",
    "        double *src_ptr = &src[0][0][0]\n",
    "        double *dst_ptr = &dst[0][pad_h][pad_w]\n",
    "        int ch\n",
    "        int c = src.shape[0]\n",
    "        int h_src = src.shape[1], w_src = src.shape[2]\n",
    "        int h_dst = dst.shape[1], w_dst = dst.shape[2]\n",
    "\n",
    "    for ch in range(c):\n",
    "        lapack.dlacpy('A', &w_src, &h_src,\n",
    "                      src_ptr, &h_src, dst_ptr, &h_dst)\n",
    "        dst_ptr += h_dst * w_dst\n",
    "        src_ptr += h_src * w_src\n",
    "\n",
    "    return\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cdef void flip(double [:,:,:,:] kernel, double [:,:,:,:] kernel_t) nogil:\n",
    "    # 一个个 channel copy\n",
    "    cdef:\n",
    "        double *src_ptr = &kernel[0][0][0][0]\n",
    "        double *dst_ptr = &kernel_t[0][0][0][0]\n",
    "        double *current_dst\n",
    "        int num_src = kernel.shape[0]\n",
    "        int c_src = kernel.shape[1]\n",
    "        int num_dst = c_src\n",
    "        int c_dst = num_src\n",
    "        int h = kernel.shape[2]\n",
    "        int w = kernel.shape[3]\n",
    "        int i, j, k, l\n",
    "\n",
    "    for i in range(num_src):\n",
    "        for j in range(c_src):\n",
    "            current_dst = dst_ptr + j * c_dst * h * w + i * h * w\n",
    "            for k in range(h):\n",
    "                for l in range(w):\n",
    "                    current_dst[(h - k - 1) * w + (w - l - 1)] = src_ptr[k * w + l]\n",
    "            src_ptr += h * w\n",
    "    return\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cpdef int backward_for_conv_batch(double [:,:,:,:] src, double[:,:,:,:] dst,\n",
    "                                  double [:,:,:,:] kernel,\n",
    "                                  double [:,:,:] pad_buf,\n",
    "                                  double [:,:] conv_buf):\n",
    "    \"\"\"\n",
    "    对于 input 的反向传播\n",
    "    tricky 的地方是必须外部提供两个分配好的 buffer，且 size 要计算好\n",
    "    \"\"\"\n",
    "    cdef:\n",
    "        int num = src.shape[0]\n",
    "\n",
    "        int c_src = src.shape[1]\n",
    "        int h_src = src.shape[2]\n",
    "        int w_src = src.shape[3]\n",
    "\n",
    "        int c_dst = dst.shape[1]\n",
    "        int h_dst = dst.shape[2]\n",
    "        int w_dst = dst.shape[3]\n",
    "\n",
    "        int kh_t = kernel.shape[3]\n",
    "        int kw_t = kernel.shape[2]\n",
    "\n",
    "        int i, j, k, l\n",
    "\n",
    "        double [:,:,:,:] kernel_t = \\\n",
    "            view.array(shape=(c_src, kernel.shape[0], kh_t, kw_t), itemsize=8, format=\"d\")\n",
    "\n",
    "        # fortran 时顺序是反的\n",
    "        int h_buf = conv_buf.shape[1]\n",
    "        int w_buf = conv_buf.shape[0]\n",
    "        int h_vec = w_buf\n",
    "        int w_vec = kernel.shape[1]\n",
    "        # 设置 stride\n",
    "        int ld_buf = h_buf\n",
    "        int ld_vec = h_vec\n",
    "        int ld_out = conv_buf.shape[1]\n",
    "        double alpha = 1.0\n",
    "        # beta 为 1 表示乘法结果是累加的，这要求 src 被 0 初始化\n",
    "        double beta = 1.0\n",
    "        double *kernel_ptr = &kernel_t[0][0][0][0]\n",
    "        double *src_ptr = &src[0][0][0][0]\n",
    "\n",
    "    with nogil:\n",
    "\n",
    "        # 先转置 kernel\n",
    "        flip(kernel, kernel_t)\n",
    "\n",
    "        # 一个个图片处理\n",
    "        for i in range(num):\n",
    "            \n",
    "            copy_and_pad(dst[i], pad_buf, kh_t - 1, kw_t - 1)\n",
    "            if im2col_core(pad_buf, conv_buf, kh_t, kw_t) != 0:\n",
    "                return -1\n",
    "            # 调用 blas 乘法 routine\n",
    "            blas.dgemm(\"N\", \"N\", &h_buf, &w_vec, &w_buf, &alpha,\n",
    "                       &conv_buf[0][0], &ld_buf, kernel_ptr, &ld_vec,\n",
    "                       &beta, src_ptr, &ld_out)\n",
    "\n",
    "            src_ptr += c_src * h_src * w_src\n",
    "\n",
    "    return 0\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cpdef int backward_kernel_for_conv_batch(double [:,:,:,:] src, double[:,:,:,:] dst,\n",
    "                                         double [:,:,:,:] kernel, double [:,:] buf) nogil:\n",
    "    \"\"\"\n",
    "    对于 kernel 的反向传播\n",
    "    \"\"\"\n",
    "    cdef:\n",
    "        int num = src.shape[0]\n",
    "        int c_src = src.shape[1]\n",
    "        int h_src = src.shape[2]\n",
    "        int w_src = src.shape[3]\n",
    "\n",
    "        int c_dst = dst.shape[1]\n",
    "        int h_dst = dst.shape[2]\n",
    "        int w_dst = dst.shape[3]\n",
    "\n",
    "        int h_kernel = kernel.shape[2]\n",
    "        int w_kernel = kernel.shape[3]\n",
    "\n",
    "        int i, j, k, l\n",
    "\n",
    "        # fortran 时顺序是反的\n",
    "        int h_buf = buf.shape[1]\n",
    "        int w_buf = buf.shape[0]\n",
    "        int h_vec = w_buf\n",
    "\n",
    "        int w_vec = c_dst\n",
    "        # 设置 stride\n",
    "        int ld_buf = h_buf\n",
    "        int ld_vec = h_vec\n",
    "        int ld_out = buf.shape[1]\n",
    "        double alpha = 1.0\n",
    "        # beta 为 1 表示乘法结果是累加的，这要求 kernel 梯度被 0 初始化\n",
    "        double beta = 1.0\n",
    "\n",
    "        double *kernel_ptr = &kernel[0][0][0][0]\n",
    "        double *src_ptr = &src[0][0][0][0]\n",
    "        double *dst_ptr = &dst[0][0][0][0]\n",
    "\n",
    "    for i in range(num):\n",
    "        \n",
    "        if im2col_n_to_one_core(src[i], buf, h_dst, w_dst) != 0:\n",
    "            return -1\n",
    "        \n",
    "        # 调用 blas 乘法 routine\n",
    "        blas.dgemm(\"N\", \"N\", &h_buf, &w_vec, &w_buf, &alpha,\n",
    "                   &buf[0][0], &ld_buf, dst_ptr, &ld_vec,\n",
    "                   &beta, kernel_ptr, &ld_out)\n",
    "        \n",
    "        dst_ptr += w_dst * h_dst * c_dst\n",
    "\n",
    "    return 0\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "<built-in function backward_for_conv_batch>\n",
      "0\n",
      "[[ 1.  2.  5.  3.]\n",
      " [ 2.  0.  3.  0.]\n",
      " [ 0.  0.  0.  4.]\n",
      " [ 5.  3.  0.  0.]\n",
      " [ 3.  0.  0.  0.]\n",
      " [ 0.  4.  0.  7.]\n",
      " [ 0.  0.  9.  3.]\n",
      " [ 0.  0.  3.  0.]\n",
      " [ 0.  7.  0.  0.]\n",
      " [ 9.  8.  1.  4.]\n",
      " [ 8.  0.  4.  1.]\n",
      " [ 0.  0.  1.  1.]\n",
      " [ 1.  4.  6.  0.]\n",
      " [ 4.  1.  0.  4.]\n",
      " [ 1.  1.  4.  3.]\n",
      " [ 6.  0.  0.  8.]\n",
      " [ 0.  4.  8.  0.]\n",
      " [ 4.  3.  0.  0.]]\n",
      "[[ 39.  18.  29.  28.]\n",
      " [ 39.  18.  29.  28.]]\n",
      "[[[[ 39.  18.]\n",
      "   [ 29.  28.]]\n",
      "\n",
      "  [[ 39.  18.]\n",
      "   [ 29.  28.]]]\n",
      "\n",
      "\n",
      " [[[ 39.  18.]\n",
      "   [ 29.  28.]]\n",
      "\n",
      "  [[ 39.  18.]\n",
      "   [ 29.  28.]]]]\n",
      "~~~~ test backprop ~~~~~\n",
      "0\n",
      "0\n",
      "[[  79.  116.  114.  141.  231.  150.  136.  150.   56.   87.  122.  114.\n",
      "   137.  232.  151.  142.  150.   60.]\n",
      " [ 116.  114.   36.  231.  150.   60.  150.   56.    7.  122.  114.   36.\n",
      "   232.  151.   57.  150.   60.    3.]\n",
      " [ 141.  231.  150.  136.  150.   56.   67.   59.    0.  137.  232.  151.\n",
      "   142.  150.   60.   58.   64.    0.]\n",
      " [ 231.  150.   60.  150.   56.    7.   59.    0.    0.  232.  151.   57.\n",
      "   150.   60.    3.   64.    0.    0.]]\n",
      "~~~~\n",
      "0\n",
      "[[ 1.  2.  5.  3.  9.  8.  1.  4.]\n",
      " [ 2.  0.  3.  0.  8.  0.  4.  1.]\n",
      " [ 0.  0.  0.  4.  0.  0.  1.  1.]\n",
      " [ 5.  3.  0.  0.  1.  4.  6.  0.]\n",
      " [ 3.  0.  0.  0.  4.  1.  0.  4.]\n",
      " [ 0.  4.  0.  7.  1.  1.  4.  3.]\n",
      " [ 0.  0.  9.  3.  6.  0.  0.  8.]\n",
      " [ 0.  0.  3.  0.  0.  4.  8.  0.]\n",
      " [ 0.  7.  0.  0.  4.  3.  0.  0.]]\n"
     ]
    }
   ],
   "source": [
    "print backward_for_conv_batch\n",
    "\n",
    "\n",
    "# 测试正确性\n",
    "# a = np.array([[[1, 2, 0, 0],\n",
    "#               [5, 3, 0, 4],\n",
    "#               [0, 0, 0, 7],\n",
    "#               [9, 3, 0, 0]],\n",
    "              \n",
    "#               [[9, 8, 0, 0],\n",
    "#               [1, 4, 1, 1],\n",
    "#               [6, 0, 4, 3],\n",
    "#               [0, 8, 0, 0]]], dtype=float)\n",
    "# aout = np.zeros((9,4))\n",
    "# print im2col2_core(a[1], aout, 3,3)\n",
    "# print aout\n",
    "# aout = np.zeros((4,9))\n",
    "# print im2col2_core(a[1], aout, 2, 2)\n",
    "# print aout\n",
    "\n",
    "a = np.array([[[[1, 2, 0, 0],\n",
    "              [5, 3, 0, 4],\n",
    "              [0, 0, 0, 7],\n",
    "              [9, 3, 0, 0]],\n",
    "              \n",
    "              [[9, 8, 0, 0],\n",
    "              [1, 4, 1, 1],\n",
    "              [6, 0, 4, 3],\n",
    "              [0, 8, 0, 0]]],\n",
    "             \n",
    "             [[[1, 2, 0, 0],\n",
    "              [5, 3, 0, 4],\n",
    "              [0, 0, 0, 7],\n",
    "              [9, 3, 0, 0]],\n",
    "              \n",
    "              [[9, 8, 0, 0],\n",
    "              [1, 4, 1, 1],\n",
    "              [6, 0, 4, 3],\n",
    "              [0, 8, 0, 0]]],\n",
    "             \n",
    "             \n",
    "             ], dtype=float)\n",
    "\n",
    "k = np.array([[[[1,1,1],[1,1,0],[1,0,0]], [[1,1,1],[1,1,0],[1,0,0]]],\n",
    "              [[[1,1,1],[1,1,0],[1,0,0]], [[1,1,1],[1,1,0],[1,0,0]]]], dtype=float)\n",
    "buf = np.zeros((18, 4))\n",
    "result = np.zeros((2,2,2, 2))\n",
    "print conv_batch(a, result, k, buf)\n",
    "print buf\n",
    "print np.dot(k.reshape((2,-1)), buf)\n",
    "print result\n",
    "print \"~~~~ test backprop ~~~~~\"\n",
    "buf2 = np.zeros((18, 16))\n",
    "pad_buf2 = np.zeros((2, 6, 6))\n",
    "print backward_for_conv_batch(a, result, k, pad_buf2, buf2)\n",
    "buf3 = np.zeros((4, 18))\n",
    "print backward_kernel_for_conv_batch(a, result, k, buf3)\n",
    "print buf3\n",
    "print \"~~~~\"\n",
    "b = np.array([[[1, 2, 0, 0],\n",
    "              [5, 3, 0, 4],\n",
    "              [0, 0, 0, 7],\n",
    "              [9, 3, 0, 0]],\n",
    "              \n",
    "              [[9, 8, 0, 0],\n",
    "              [1, 4, 1, 1],\n",
    "              [6, 0, 4, 3],\n",
    "              [0, 8, 0, 0]]]\n",
    "             , dtype=float)\n",
    "buf4 = np.zeros((9, 8))\n",
    "print im2col_n_to_one_core(b, buf4, 3, 3)\n",
    "print buf4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# a = np.random.random((1, 100, 100))\n",
    "# aout = np.zeros((25, 96 * 96))\n",
    "# %timeit im2col(a, aout, 5, 5)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "a = np.random.random((20, 3, 128, 128))\n",
    "buf = np.zeros((11 * 11 * 3, 118 * 118))\n",
    "out = np.zeros((20, 96, 118, 118))\n",
    "k = np.random.random((96, 3, 11, 11))\n",
    "%timeit conv_batch(a, out, k, buf)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "%%cython\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 2",
   "language": "python",
   "name": "python2"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 2
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython2",
   "version": "2.7.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
